config {
  type: "incremental",
  schema: "stage",
  uniqueKey: ["article_code", "supplier_code"],
  bigquery: { partitionBy: "first_seen" }
}

WITH src AS (
  -- daily sales
  SELECT
    article_code,
    supplier_code,
    MIN(sale_date) AS first_seen,
    MAX(sale_date) AS last_seen,
    CAST('sales_daily' AS STRING) AS src_label
  FROM ${ref("stage","stage_sales_daily")}
  WHERE article_code IS NOT NULL AND supplier_code IS NOT NULL
  GROUP BY article_code, supplier_code

  UNION ALL

  -- stock in transit
  SELECT
    article_code,
    supplier_code,
    MIN(in_transit_date) AS first_seen,
    MAX(in_transit_date) AS last_seen,
    CAST('stock_in_transit' AS STRING) AS src_label
  FROM ${ref("stage","stage_stock_in_transit_daily")}
  WHERE article_code IS NOT NULL AND supplier_code IS NOT NULL
  GROUP BY article_code, supplier_code

  UNION ALL

  -- monthly sales: convert "MM/YYYY" -> DATE "YYYY-MM-01"
  SELECT
    article_code,
    supplier_code,
    MIN(DATE(SAFE.PARSE_DATE('%d/%m/%Y', CONCAT('01/', sale_month)))) AS first_seen,
    MAX(DATE(SAFE.PARSE_DATE('%d/%m/%Y', CONCAT('01/', sale_month)))) AS last_seen,
    CAST('sales_monthly' AS STRING) AS src_label
  FROM ${ref("stage","stage_sales_monthly")}
  WHERE article_code IS NOT NULL AND supplier_code IS NOT NULL
  GROUP BY article_code, supplier_code
),
u AS (
  SELECT
    article_code,
    supplier_code,
    MIN(first_seen) AS first_seen,
    MAX(last_seen)  AS last_seen,
    ARRAY_AGG(DISTINCT src_label) AS sources
  FROM src
  GROUP BY article_code, supplier_code
)
SELECT * FROM u
${when(incremental(), "WHERE first_seen >= DATE_SUB(CURRENT_DATE(), INTERVAL 30 DAY)")}
