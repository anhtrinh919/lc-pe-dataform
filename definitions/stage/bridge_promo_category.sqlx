config {
  type: "incremental",
  schema: "stage",
  name: "bridge_promo_category",
  tags: ["bridge","promo"],
  bigquery: {
    partitionBy: "first_seen",
    clusterBy: ["subdept_code"]
  }
}

WITH d AS (
  SELECT
    CAST(subdept_code AS STRING)      AS subdept_code,
    MIN(sale_date)               AS first_seen,
    MAX(sale_date)               AS last_seen,
    'promo_daily'                AS src_label
  FROM ${ref("stage","stage_promo_daily")}
  WHERE subdept_code IS NOT NULL AND subdept_code != ''
  GROUP BY subdept_code
),
v AS (
  SELECT
    CAST(subdept_code AS STRING)      AS subdept_code,
    MIN(sale_date)               AS first_seen,
    MAX(sale_date)               AS last_seen,
    'promo_vll'                  AS src_label
  FROM ${ref("stage","stage_promo_vll")}
  WHERE subdept_code IS NOT NULL AND subdept_code != ''
  GROUP BY subdept_code
),
src AS (
  SELECT * FROM d
  UNION ALL
  SELECT * FROM v
),
u AS (
  SELECT
    subdept_code,
    MIN(first_seen) AS first_seen,
    MAX(last_seen)  AS last_seen,
    ARRAY_AGG(DISTINCT src_label) AS sources
  FROM src
  GROUP BY subdept_code
)
SELECT * FROM u
${when(incremental(), "WHERE first_seen >= DATE_SUB(CURRENT_DATE(), INTERVAL 30 DAY)")}