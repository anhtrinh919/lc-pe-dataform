config {
  type: "incremental",
  schema: "stage",
  uniqueKey: ["supplier_code"]
}

WITH sup_month AS (
  SELECT
    supplier_code,
    ANY_VALUE(supplier_name) AS supplier_name,
    MIN(DATE(SAFE.PARSE_DATE('%d/%m/%Y', CONCAT('01/', sale_month)))) AS first_seen,
    MAX(DATE(SAFE.PARSE_DATE('%d/%m/%Y', CONCAT('01/', sale_month)))) AS last_seen
  FROM ${ref("stage","stage_sales_monthly")}
  WHERE supplier_code IS NOT NULL AND supplier_code != ''
  GROUP BY supplier_code
),
sup_intransit AS (
  SELECT
    supplier_code,
    ANY_VALUE(supplier_name) AS supplier_name,
    MIN(in_transit_date) AS first_seen,
    MAX(in_transit_date) AS last_seen
  FROM ${ref("stage","stage_stock_in_transit_daily")}
  WHERE supplier_code IS NOT NULL AND supplier_code != ''
  GROUP BY supplier_code
),
u AS (
  SELECT
    supplier_code,
    ANY_VALUE(supplier_name) AS supplier_name,
    MIN(first_seen) AS first_seen,
    MAX(last_seen)  AS last_seen
  FROM (
    SELECT * FROM sup_month
    UNION ALL
    SELECT * FROM sup_intransit
  )
  GROUP BY supplier_code
)
SELECT * FROM u

${when(incremental(),
`WHERE supplier_code NOT IN (SELECT supplier_code FROM ${self()})`)}
