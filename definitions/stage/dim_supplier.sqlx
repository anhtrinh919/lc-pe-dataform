config {
  type: "incremental",
  schema: "stage",
  uniqueKey: ["supplier_code"]
}

with sup_month as (
  select supplier_code, any_value(supplier_name) as supplier_name,
         min(sale_month) as first_seen, max(sale_month) as last_seen
  from ${ref("stage","stage_sales_monthly")}
  where supplier_code is not null and supplier_code != ''
  group by supplier_code
),
sup_intransit as (
  select supplier_code, any_value(supplier_name) as supplier_name,
         min(in_transit_date) as first_seen, max(in_transit_date) as last_seen
  from ${ref("stage","stage_stock_in_transit_daily")}
  where supplier_code is not null and supplier_code != ''
  group by supplier_code
),
u as (
  select supplier_code,
         any_value(supplier_name) as supplier_name,
         min(first_seen) as first_seen,
         max(last_seen)  as last_seen
  from (
    select * from sup_month
    union all
    select * from sup_intransit
  )
  group by supplier_code
)
select * from u

${when(incremental(),
`where supplier_code not in (select supplier_code from ${self()})`)}