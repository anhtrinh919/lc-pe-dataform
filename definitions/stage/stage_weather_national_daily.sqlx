config {
  type: "incremental",
  schema: "stage",
  tags: ["weather"],
  bigquery: { partitionBy: "weather_date" }
}

WITH agg AS (
  SELECT
    weather_date,
    AVG(tmax_c)                        AS avg_tmax_c,
    AVG(tmin_c)                        AS avg_tmin_c,
    AVG(precip_mm)                     AS avg_precip_mm,
    AVG(COALESCE(rain_mm, precip_mm))  AS avg_rain_mm,
    MAX(windmax_ms)                    AS max_windmax_ms
  FROM ${ref("stage_weather_daily")}
  GROUP BY weather_date
),
final AS (
  SELECT
    weather_date,
    (avg_tmax_c + avg_tmin_c)/2        AS tavg_c,
    avg_tmin_c                          AS tmin_c,
    avg_tmax_c                          AS tmax_c,
    avg_precip_mm                       AS precip_mm,
    avg_rain_mm                         AS rain_mm,
    max_windmax_ms                      AS windmax_ms
  FROM agg
)
SELECT * FROM final
${when(incremental(), `
  WHERE weather_date >
    (SELECT COALESCE(MAX(weather_date), DATE '1900-01-01') FROM ${self()})
`)}
