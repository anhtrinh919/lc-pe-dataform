config {
  type: "incremental",
  schema: "fact",
  tags: ["price","daily"],
  bigquery: {
    partitionBy: "sale_date",
    clusterBy: ["store_id","article_code","channel"]
  }
}

WITH retail AS (
  SELECT
    SAFE_CAST(sale_date AS DATE) AS sale_date,
    store_id,
    article_code,
    "RETAIL" AS channel,
    SUM(quantity) AS qty,
    SUM(turnover) AS turnover
  FROM ${ref("stage_sales_daily")}
  GROUP BY 1,2,3,4
),
b2b AS (
  SELECT
    SAFE_CAST(sale_date AS DATE) AS sale_date,
    store_id,
    article_code,
    "B2B" AS channel,
    SUM(quantity) AS qty,
    SUM(turnover) AS turnover
  FROM ${ref("stage_b2b_sales_daily")}
  GROUP BY 1,2,3,4
),
u AS (
  SELECT * FROM retail
  UNION ALL
  SELECT * FROM b2b
)
SELECT
  sale_date,
  store_id,
  article_code,
  channel,
  qty AS units_sold,
  turnover AS revenue,
  SAFE_DIVIDE(turnover, NULLIF(qty,0)) AS unit_price_observed
FROM u
${when(incremental(), `
WHERE sale_date >
  (SELECT COALESCE(MAX(sale_date), DATE '1900-01-01') FROM ${self()})
`)}
