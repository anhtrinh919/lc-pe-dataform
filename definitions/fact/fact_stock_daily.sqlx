config {
  type: "incremental",
  schema: "fact",
  uniqueKey: ["stock_date","store_id","article_code"],
  bigquery: { partitionBy: "stock_date" },
  tags: ["stock","onhand","daily","canonical"]
}

SELECT
  sd.stock_date,
  sd.store_id,            -- canonical
  sd.article_code,        -- canonical
  sd.stock_quantity       AS on_hand_qty,
  sd.stock_value,
  SAFE_DIVIDE(sd.stock_value, NULLIF(sd.stock_quantity,0)) AS unit_cost_est,
  (IFNULL(sd.stock_quantity, 0) <= 0) AS is_oos,
  'stage_stock_daily' AS src_table
FROM ${ref("stage_stock_daily")} sd
WHERE TRUE
${ when(incremental(), `
  AND sd.stock_date > (
    SELECT IFNULL(MAX(stock_date), DATE '1900-01-01') FROM ${self()}
  )
`) }