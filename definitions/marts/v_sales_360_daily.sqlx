config {
  type: "view",
  schema: "marts",
  tags: ["sales","daily","360","ai-entry"]
}

/* Grain: date × store_id × article_code (DAILY only) */
WITH d AS (
  SELECT
    s.sale_date AS date,
    s.store_id,
    s.article_code,
    s.quantity,
    s.turnover,
    s.vat_amount,
    s.cost_value,
    s.margin_value
  FROM ${ref("fact_sales_daily")} s
),

agg AS (
  SELECT
    date,
    store_id,
    article_code,
    SUM(quantity)     AS qty,
    SUM(turnover)     AS turnover,
    SUM(vat_amount)   AS vat_amount,
    SUM(cost_value)   AS cost_value,
    SUM(margin_value) AS margin_value,
    SAFE_DIVIDE(SUM(margin_value), NULLIF(SUM(turnover),0)) AS margin_percent
  FROM d
  GROUP BY date, store_id, article_code
),

store AS (
  SELECT store_id, store_name, province, district
  FROM ${ref("dim_store")}
),
prod AS (
  SELECT article_code, article_description, sv, division, department, fam_group, family, sub_family
  FROM ${ref("dim_product")}
)

SELECT
  a.date,
  'DAY'        AS grain,
  a.store_id,
  st.store_name,
  st.province,
  st.district,
  a.article_code,
  p.article_description,
  p.sv, p.division, p.department, p.fam_group, p.family, p.sub_family,
  a.qty,
  a.turnover,
  a.vat_amount,
  a.cost_value,
  a.margin_value,
  a.margin_percent
FROM agg a
LEFT JOIN store st USING (store_id)
LEFT JOIN prod  p  USING (article_code)